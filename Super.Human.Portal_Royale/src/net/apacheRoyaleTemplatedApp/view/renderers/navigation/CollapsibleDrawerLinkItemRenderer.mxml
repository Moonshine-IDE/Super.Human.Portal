<?xml version="1.0" encoding="utf-8"?>
<j:CollapsibleNavigationSectionRenderer xmlns:fx="http://ns.adobe.com/mxml/2009" 
									    xmlns:j="library://ns.apache.org/royale/jewel" 
									    xmlns:js="library://ns.apache.org/royale/basic" 
									    xmlns:html="library://ns.apache.org/royale/html" 
									    xmlns="http://www.w3.org/1999/xhtml"
									    visible="{dataVO.visible}" 
									    sectionClick="onRendererSectionClick(event)">
	<j:beads>
		<js:ItemRendererDataBinding />
	</j:beads>
	<fx:Script>
		<![CDATA[
			import org.apache.royale.jewel.Navigation;
			import model.vo.NavigationLinkVO;

			private var _childNavClassName:String =  "navigation-section-group";

			override public function get childNavClassName():String
			{
				return _childNavClassName + " navIconLinkItemRenderer";
			}
				
			override public function get retainHover():Boolean
			{
            		return true;
        		}
				
			override public function set data(value:Object):void
			{
				super.data = value;

				if (value.visible)
				{
					refreshEnabled();
					refreshExpandIcon();
					
					this.open = value.open;
					
					if (dataVO.subMenu)
					{
						if (!dataVO.subMenu.hasEventListener("lengthChanged"))
						{
							dataVO.subMenu.addEventListener("lengthChanged", onSubMenuLengthChanged);
						}
					}
				}
			}
	
			[Bindable("dataChange")]
            public function get dataVO():NavigationLinkVO
            {
                return data as NavigationLinkVO;
            }
            
        		override public function onChange(selectedItem:Object):void
        		{
        			super.onChange(selectedItem);
        			
	            var parentNav:Navigation = this.itemRendererOwnerView ? this.itemRendererOwnerView.host as Navigation : null;
	            if (parentNav) 
	            {
	                //this hierarchy support is a bit kooky, and probably there will be something better later with Hierarchical data setup
	                data.selectedChild = selectedItem;
	                if (parentNav.selectedItem != this.data) 
	                {
	                    parentNav.selectedItem = this.data;
	                }
	                parentNav.dispatchEvent(new Event('change'));
	            }
	            
	            refreshExpandIcon();
	        }			
        		
        		override public function getSelectedSubmenuItem():Object
        		{
        			if (childNavigation) 
        			{
        				return childNavigation.selectedItem;
				}        			
				return null;	
        		}
        		      
        		protected function onSubMenuLengthChanged(event:Event):void
        		{
        			refreshExpandIcon();
        		}
        		  		
        		protected function onRendererSectionClick(event:Event):void
        		{
	            if (event.target == this)
	            {
	                this.open = !this.open;
	                
	                if (this.data)
	                {
	                		this.data.open = this.open;
	                }
	            }
	            
	            refreshExpandIcon();
        		}
        		
        		private function refreshExpandIcon():void
        		{
        			expandIcon.visible = dataVO.subMenu != null && dataVO.subMenu.length > 0;	
        			if (expandIcon.visible)
        			{
        				this.removeClass("no-submenu")
        			}
        			else
        			{
      				if (!this.containsClass("no-submenu"))
        				{
        					this.addClass("no-submenu");
    					}
        			}
        		}
        		
			private function refreshEnabled():void
			{			
				if (data && data.enabled == false)
				{
					this.element.style["pointerEvents"] = "none";
				}
				else
				{
					this.element.style["pointerEvents"] = "";
				}
			}
			
			public function unsetSelectedSubmenuItem():void 
			{
				if (this.getSelectedSubmenuItem() && childNavigation) 
				{
					childNavigation.selectedItem = null;
				}
				
				refreshExpandIcon();
			}
		]]>
	</fx:Script>
	
	<j:HGroup gap="2" itemsVerticalAlign="itemsCenter" height="48">
		<html:Span class="{dataVO ? dataVO.icon : ''}" visible="{dataVO ? dataVO.icon != null : false}"/>
		
		<html:Span text="{dataVO ? dataVO.name : ''}"/>
		<js:MaterialIcon localId="expandIcon" text="{open? MaterialIconType.EXPAND_LESS: MaterialIconType.EXPAND_MORE}" className="hint" />
	</j:HGroup>
</j:CollapsibleNavigationSectionRenderer>