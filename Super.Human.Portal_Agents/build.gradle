/*
Build script for Super.Human.Portal_Agents
*/
plugins {
    id 'java'
    //id 'application'  // allows 'run' task.
    id 'eclipse'      // allows dependencies to be exported to .classpath
    id 'groovy'
}

archivesBaseName='Super.Human.Portal_Agents'

String notesInstallation = project.findProperty('notesInstallation') ?: '/Applications/HCL Notes.app/Contents/MacOS/'
if (!notesInstallation) {
	throw new GradleException("Missing configured path for Notes installation.  Set notesInstallation in gradle.properties.")
}
else if (!(new File(notesInstallation).exists())) {
	throw new GradleException("Invalid configured path for Notes installation ($notesInstallation).  Check notesInstallation in gradle.properties.")
}


String notesJarBaseDir = "$notesInstallation"
String envPath = System.getenv('PATH')
if (!envPath) {
	logger.warn "Missing PATH environment variable."
	envPath = ''  // default to empty string to avoid null errors later
}


// change envPath as needed
if (notesInstallation.toLowerCase().startsWith('/applications/')) { // treat as macOS
        // directory for additional JARs is inconsistent across version, so just use `/Applications/HCL Notes.app/Contents`
        notesJarBaseDir = "$notesInstallation/.."
}
else { // treat as Windows
	// update PATH
	envPath = "$envPath;$notesInstallation"
	println "Updated PATH: $envPath"
}

/**
 * Method to find a file under the given path
 * @param baseDir  the base directory to search
 * @param findName  the name of the file to find.  TODO:  support patterns
 * @return the absolute path of the file, or null if the file was not found.
 */
String findFile(String baseDir, String findName) {
    String foundFile = null;
    // search for a file with the given pattern.  If multiple copies are found, use the last
    new File(baseDir).eachFileRecurse {File curFile ->
        if (curFile.getName() == findName) {
            foundFile = curFile.getAbsolutePath()
            println "Found file '$findName' at '${curFile.getAbsolutePath()}'"
        }
    }

    if (!foundFile) {
        println ("Could not find '$findName' in '$baseDir'")
    }

    return foundFile
}

// build a jar path to use for Notes.jar
// Supports local and Linux server executions
String notesJarPath = findFile(notesJarBaseDir, 'Notes.jar')
String jarClassPath = "./Notes.jar {{ domino_install_dir }}/jvm/lib/ext/Notes.jar ${notesJarPath.replaceAll(' ', '%20').replaceAll('^\\w+:', '/$0')}"

repositories {
    mavenCentral()
}
sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src/main/generated']
        }
    }
}

// Required for Java 8 Language server support
sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
	// custom environment for DXL Importer
	dxlRuntime.extendsFrom implementation
}

dependencies {
    // CRUD API library
    implementation files('lib/Moonshine-Domino-CRUD.jar')

    
    // Local JAR dependency
    implementation files(notesJarPath)
    // the paths to these files vary for the different Domino installations, so search for them instead

    // DXL Importer
    dxlRuntime files('dxlLib/ImportDxlNsf.jar')
    
    // Dependencies for DXL Importer
    dxlRuntime 'org.tmatesoft.svnkit:svnkit:1.8.5'
    dxlRuntime files(findFile(notesJarBaseDir, 'websvc.jar'))
    // New Dependencies for Domino 12.0.2
    implementation files(findFile(notesJarBaseDir, 'lwpd.domino.napi.jar'))
    implementation files(findFile(notesJarBaseDir, 'lwpd.commons.jar'))
    
    // Dependencies for SimpleHTTPClient
    implementation 'commons-codec:commons-codec:1.4'
    implementation 'commons-httpclient:commons-httpclient:3.1'  //newer versions in com.apache.httpcomponents
    implementation 'org.apache.commons:commons-lang3:3.0.1'
    implementation 'commons-logging:commons-logging:1.1.1'

    
    // Use the latest Groovy version for Spock testing
    testImplementation 'org.codehaus.groovy:groovy-all:2.5.6'
    // Use the Spock testing and specification framework even with Java
    testImplementation 'org.spockframework:spock-core:1.2-groovy-2.5'
    testImplementation 'junit:junit:4.12'
}

/*
 * Update the logic to build a fat jar (excluding Notes.jar) for import into script library.
 * This template will upload the JAR dependencies (only Moonshine-Domino-CRUD.jar) as separate script libraries, 
 * which will allow the library to be updated separately from the agents.
 * If you have additional dependencies to support, you can create more script libraries or enable the code below.
 */
// TODO:  not working in Gradle 7.5.1
// jar {
// 	
// 	from { 
// 		// runtimeClasspath includes implementation and runtimeOnly dependencies
// 		// findResults allows using null to exclude results
// 		configurations.runtimeClasspath.findResults { File dependency ->
// 			if (dependency.getName().equalsIgnoreCase("Notes.jar")) {
// 				return null  // skip this entry
// 			}
// 			else if (dependency.getName().equalsIgnoreCase("Moonshine-Domino-CRUD.jar")) {
// 				return null  // skip this entry - it is covered by the Moonshine-Domino-CRUD script library
// 			}
// 			else {
// 				println dependency.getName()
// 				return dependency.isDirectory() ? dependency : zipTree(dependency) 
// 			}
// 		} 
// 		
// 		duplicatesStrategy DuplicatesStrategy.EXCLUDE
// 		// setDuplicatesStrategy(DuplicatesStrategy.INCLUDE)
// 	}
// }

task copyDependencies(type: Copy) {
   from configurations.runtimeClasspath
   into 'dependencies'
}


/* #### DXL Importer Configuration #### */


/**
 * Custom agent for running Java with Domino Dependencies
 * Set environment variables based on NotesInstallation.
 */
class RunDomino extends JavaExec {
	
	public RunDomino() {
		super()
		classpath = project.sourceSets.main.runtimeClasspath
		addDominoEnvironment()

		// map STDERR to STDOUT to make the output more clear in Moonshine
		errorOutput = System.out

		String notesIDPassword = project.findProperty('notesIDPassword')
		if (notesIDPassword) {
			// pass the password by STDIN
			// If there is a prompt for an invalid password, escape the prompt with Esc
			standardInput = new java.io.StringBufferInputStream("${notesIDPassword}\n\u001B")
		}
		else {
			// Send the Esc key to cancel any unexpected password prompts
			standardInput = new java.io.StringBufferInputStream("\u001B")
		}
	}
	
	public addDominoEnvironment() {
		String notesInstallation = project.findProperty('notesInstallation')
		String envPath = System.getenv('PATH')
		if (!envPath) {
			logger.warn "Missing PATH environment variable."
			envPath = ''  // default to empty string to avoid null errors later
		}
		
		// change envPath as needed
		if (notesInstallation.toLowerCase().startsWith('/applications/')) { // treat as macOS
			// no special logic is required
		}
		else { // treat as Windows
			// update PATH
			envPath = "$envPath;$notesInstallation"
		}
		environment('DYLD_LIBRARY_PATH', notesInstallation)
		environment('PATH', envPath)
	}
}

/** 
 * Custom Task Type for DXL Importer.
 * Allows setting some of the JavaExec properties in a reusable task
 * I based this on: https://www.baeldung.com/gradle-custom-task
 */
class ImportAgent extends RunDomino {
	@Input
	String propertiesFile
	@Input
	String type = 'agent' // "agent" or "scriptlibrary" - make this an enum?
	@Input
	String server = project.findProperty('server')
	@Input
	String dbName = project.findProperty('dbName')
	
	public ImportAgent() {
		super()
		classpath = project.configurations.dxlRuntime
		main = 'com.Prominic.ImportDxlNsfForStatic'
		
	}
	
	@TaskAction
	@Override
	void exec() {
		
		// validation
		if (!propertiesFile) {
			throw new GradleException("No defined agent properties file.")
		}
		if (!server) {
			throw new GradleException("No Domino server defined.  Use `-Pserver` or `gradle.properties`.")
		}
		if (!dbName) {
			throw new GradleException("No Domino dbName defined.  Use `-PdbName` or `gradle.properties`.")
		}
		
		// set args here so that it can use the configured properties
		args '-a', propertiesFile?:'undefined', 
		     '-t', this.type, 
		     '-sp', project.findProperty('scriptLibraryConfig')?:'scriptLibraryConfig/ScriptLibraryConfig.properties', 
		     '-b', '.', 
		     '-s', server, 
		     '-n', dbName
		super.exec()
		logger.warn 'NOTE:  You can ignore the SVNException error above.  This will be removed in a later version of DXLImporter'
	}
}

task importAllScriptLibraries() {
	// placeholder for dependencies generated by below code
}

task importAllAgents(dependsOn:  tasks.getByName('importAllScriptLibraries')) {
	// placeholder for dependencies generated by below code
	// Used to enforce that importAllScriptLibraries runs before the agents, without requring the script libraries for each agent.
}

task importAll(dependsOn: [tasks.getByName('importAllScriptLibraries'), tasks.getByName('importAllAgents')]) {
	// further dependencies should be added to importAllScriptLibraries or importAllAgents
}

task jarApacheCommons( dependsOn: copyDependencies) {
	// This script library does not have any local code.  Instead, it imports some JARs from the dependencies.
	// The JAR names need to be specified exactly in agentProperties/genesis/GenesisRead.properties
}

// generate tasks for agents
String agentPropertiesDirName = project.findProperty('agentPropertiesDir') ?: 'agentProperties/'
new File(agentPropertiesDirName).eachFileRecurse groovy.io.FileType.FILES, {File curFile ->
	String fullName = curFile.getName()
	int splitIndex = fullName.lastIndexOf('.')

	if (splitIndex >= 0) {  // confirm this is a file with the expected format
		String extension = fullName.substring(splitIndex+1)
		String agentName = fullName.substring(0, splitIndex)
		// treat any files in the "scriptbuild" directory as script libraries.  Improve this logic if needed.
		boolean isScriptLibrary = (curFile.getAbsolutePath().toLowerCase().indexOf('scriptbuild') > 0) // default all other files to be agents

		if (extension.equalsIgnoreCase('properties')) {
			String importTaskName = "import$agentName"
			String jarTaskName = "jar$agentName"
			// if the custom jar task doesn't exist, default to base compile
			jarTaskName = tasks.findByName(jarTaskName) ? jarTaskName : 'jar'

			// don't override the existing task if it exists - allow this so that the user can define a custom override
			if (!tasks.findByName(importTaskName)) {
				tasks.create(name: importTaskName, type: ImportAgent, dependsOn: tasks.getByName(jarTaskName) ) {
					propertiesFile = curFile.getAbsolutePath()
					type = isScriptLibrary ? 'scriptlibrary' : 'agent'
				}
			}
			// else:  the task already exists

			// add a dependency on the importAll task
			if (isScriptLibrary) {
				tasks.getByName('importAllScriptLibraries').dependsOn << importTaskName
			}
			else {  // agent
				tasks.getByName('importAllAgents').dependsOn << importTaskName
			}


		}
		else {
			println "Ignoring invalid property file:  ${curFile.getAbsolutePath()}"
		}
	}
}

